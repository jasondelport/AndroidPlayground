apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'me.tatarka.retrolambda'
apply from: '../config/quality.gradle'

retrolambda {
    jdk JAVA8_HOME
    oldJdk JAVA_HOME
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    def Properties properties = new Properties()

    defaultConfig {
        applicationId "com.jasondelport.playground"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "1.1"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [GOOGLE_GEO_API_KEY: "AIzaSyAmtcM6dHT-zkwgKJZuefum0bP7OdFxQ1g"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('debug.jks')
            storePassword 'android'
            keyAlias 'debug'
            keyPassword 'android'
        }
    }

    buildTypes {

        debug {
            properties.load(new FileInputStream(file('/Users/jasondelport/Code/secure/AndroidPlayground.properties')))
            buildConfigField "String", "URL", properties['url']
            resValue "string", "url", properties['url']
            resValue "string", "build_time", buildTime
            resValue "string", "git_revision", "\"${gitRevision()}\""
            debuggable true
        }

        release {
            properties.load(new FileInputStream(file('/Users/jasondelport/Code/secure/AndroidPlayground.properties')))
            buildConfigField "String", "URL", properties['url']
            resValue "string", "url", properties['url']
            resValue "string", "build_time", buildTime
            resValue "string", "git_revision", "\"${gitRevision()}\""
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        quiet true
        abortOnError false
        checkReleaseBuilds false
        ignoreWarnings true
        warning 'InvalidPackage'
        xmlReport false
        htmlReport true
        lintConfig file("${project.rootDir}/config/quality/lint/lint.xml")
        htmlOutput file("$project.buildDir/reports/lint/lint-result.html")
        xmlOutput file("$project.buildDir/reports/lint/lint-result.xml")
    }


    task('updateIcon') << {
        def caption = "v" + defaultConfig.versionName.toString()
        exec {
            executable 'bash'
            args "$rootProject.rootDir/scripts/icon-gen.sh", caption, rootProject.rootDir, '/scripts'
        }
    }

    updateIcon.doLast{
        println 'Finished generating icon'
    }

    //preBuild.dependsOn updateIcon

}

def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}

task('copyAll') {
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (!output.outputFile.name.contains('unsigned')) {
                task("copy${variant.name.capitalize()}", type: Copy) {
                    println output.outputFile
                    from(output.outputFile)
                    into("/Users/jasondelport/Downloads/")
                }
                dependsOn("copy${variant.name.capitalize()}")
            }
        }
    }
}

task('installAllExceptRelease') {
    android.applicationVariants.all { variant ->
        if (!variant.name.equals('release')) {
            dependsOn("install${variant.name.capitalize()}")
        }
        /*
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            println outputFile // full path
            println outputFile.name // just the file name
            if (!outputFile.name.contains('unsigned')) {
                dependsOn("install${variant.name.capitalize()}")
            }
        }
        */
    }
}

task('runDebug', type: Exec, dependsOn: 'installDebug') {
    def adb = android.getAdbExe().toString()
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.jasondelport.playground.debug/com.jasondelport.playground.ui.activity.MainActivity'
    doLast {
        println 'Finished launching application'
    }
}
/*
task('runRelease', type: Exec, dependsOn: 'installRelease') {
    def adb = android.getAdbExe().toString()
    commandLine "$adb", 'shell', 'am', 'start', '-n', 'com.jasondelport.playground/.ui.activity.MainActivity'
    doFirst {
        println '=== start ==='
    }
    doLast {
        println '=== end ==='
    }
}

*/
// ./gradlew dependencyUpdates
// ./gradlew dependencyUpdates -Drevision=release
// ./gradlew build --refresh-dependencies
// ./gradlew :app:dependencies
// ./gradlew :app:androidDependencies.
dependencies {

    def GOOGLE_LIBRARY_VERSION = '23.4.0'
    // google
    compile "com.android.support:support-v13:$GOOGLE_LIBRARY_VERSION"
    compile "com.android.support:design:$GOOGLE_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$GOOGLE_LIBRARY_VERSION"
    compile "com.android.support:appcompat-v7:$GOOGLE_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$GOOGLE_LIBRARY_VERSION"
    compile "com.android.support:percent:$GOOGLE_LIBRARY_VERSION"
    compile "com.android.support:support-annotations:$GOOGLE_LIBRARY_VERSION"
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.google.android.gms:play-services-maps:9.0.0'
    compile 'com.google.android.gms:play-services-location:9.0.0'
    compile 'com.google.android:flexbox:0.1.2'

    // rx java
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'

    // square
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.2.0'
    compile 'com.squareup.picasso:picasso:2.5.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.squareup:otto:1.3.8' // no longer in active development
    compile 'com.squareup.moshi:moshi:1.1.0'


    // jake wharton
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'com.jakewharton:disklrucache:2.0.2'

    // cache
    compile 'com.anupcowkur:reservoir:2.1'

    // auto values
    provided 'com.google.auto.value:auto-value:1.2' // needed for Android Studio
    apt 'com.google.auto.value:auto-value:1.2'
    apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.1'

    // saved instance state
    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'
    compile 'org.parceler:parceler-api:1.1.3'
    provided 'org.parceler:parceler:1.1.3'

    //dagger
    compile 'com.google.dagger:dagger:2.4'
    apt 'com.google.dagger:dagger-compiler:2.4'
    compile 'javax.annotation:javax.annotation-api:1.2'

    //permissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'

    // captures ANR errors
    compile 'com.github.anrwatchdog:anrwatchdog:1.2.0'

    // RX Location
    compile 'pl.charmas.android:android-reactive-location:0.9@aar'

    // robolectric
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.hamcrest:hamcrest-integration:1.3'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile ('org.robolectric:robolectric:3.0') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    // espresso
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile "com.android.support:support-annotations:$GOOGLE_LIBRARY_VERSION"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    // for intent mocking
    //androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.1'
    // for webview testing
    //androidTestCompile 'com.android.support.test.espresso:espresso-web:2.2.1'

}

/*
Android tasks
-------------
androidDependencies - Displays the Android dependencies of the project.
signingReport - Displays the signing info for each variant.

Build tasks
-----------
assemble - Assembles all variants of all applications and secondary packages.
assembleAndroidTest - Assembles all the Test applications.
assembleDebug - Assembles all Debug builds.
assembleDebugAndroidTest - Assembles the android (on device) tests for the Debug build.
assembleRelease - Assembles all Release builds.
build - Assembles and tests this project.
buildDependents - Assembles and tests this project and all projects that depend on it.
buildNeeded - Assembles and tests this project and all projects it depends on.
clean - Deletes the build directory.
compileDebugAndroidTestSources
compileDebugSources
compileDebugUnitTestSources
compileReleaseSources
compileReleaseUnitTestSources
mockableAndroidJar - Creates a version of android.jar that's suitable for unit tests.

Build Setup tasks
-----------------
init - Initializes a new Gradle build. [incubating]

Help tasks
----------
components - Displays the components produced by root project 'complete'. [incubating]
dependencies - Displays all dependencies declared in root project 'complete'.
dependencyInsight - Displays the insight into a specific dependency in root project 'complete'.
help - Displays a help message.
projects - Displays the sub-projects of root project 'complete'.
properties - Displays the properties of root project 'complete'.
tasks - Displays the tasks runnable from root project 'complete'.

Install tasks
-------------
installDebug - Installs the Debug build.
installDebugAndroidTest - Installs the android (on device) tests for the Debug build.
uninstallAll - Uninstall all applications.
uninstallDebug - Uninstalls the Debug build.
uninstallDebugAndroidTest - Uninstalls the android (on device) tests for the Debug build.
uninstallRelease - Uninstalls the Release build.

Verification tasks
------------------
check - Runs all checks.
connectedAndroidTest - Installs and runs the tests for Debug build on connected devices.
connectedCheck - Runs all device checks on currently connected devices.
deviceCheck - Runs all device checks using Device Providers and Test Servers.
lint - Runs lint on all variants.
lintDebug - Runs lint on the Debug build.
lintRelease - Runs lint on the Release build.
test - Run all unit tests.
testDebug - Run unit tests for the Debug build.
testRelease - Run unit tests for the Release build.
 */